apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                javanano {
                    // Options added to --javanano_out
                    option 'ignore_services=true'
                    option 'store_unknown_fields=true'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'nano=true'
                }
            }
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    publishNonDefault true

    productFlavors {
        prod {
            buildConfigField 'String', 'HOST', '"honarnama.net"'
            buildConfigField 'int', 'SECURE_PORT', "8000"
            buildConfigField 'int', 'INSECURE_PORT', "8001"
        }

        stg1 {
            buildConfigField 'String', 'HOST', '"staging1.honarnama.net"'
            buildConfigField 'int', 'SECURE_PORT', "0"
            buildConfigField 'int', 'INSECURE_PORT', "8001"
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.parse.bolts:bolts-android:1.4.0'
    compile 'com.makeramen:roundedimageview:2.1.2'
    compile 'com.mikepenz:iconics-core:2.5.5@aar'
    compile 'com.mikepenz:google-material-typeface:2.1.0.1.original@aar'
    compile 'com.android.support:design:23.2.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'io.grpc:grpc-okhttp:1.0.0'
    compile 'io.grpc:grpc-protobuf-nano:1.0.0'
    compile 'io.grpc:grpc-stub:1.0.0'
}

apply plugin: 'com.google.gms.google-services'